{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1e67e2a7",
   "metadata": {},
   "outputs": [],
   "source": [
    "## Packages\n",
    "\n",
    "using Random\n",
    "using Distributions\n",
    "using Plots\n",
    "using Printf\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "482c9ce5",
   "metadata": {},
   "outputs": [],
   "source": [
    "## Random seed for reproducibility\n",
    "\n",
    "USE_RANDOM_SEED = false\n",
    "RANDOM_SEED_VALUE = 314\n",
    "if USE_RANDOM_SEED\n",
    "    Random.seed!(RANDOM_SEED_VALUE)\n",
    "end\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "53953b02",
   "metadata": {},
   "outputs": [],
   "source": [
    "## Game parameters\n",
    "\n",
    "K = 7    # Number of bandit arms\n",
    "T = 1000 # Number of timesteps\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5a004b20",
   "metadata": {},
   "outputs": [],
   "source": [
    "## Distribution parameters\n",
    "\n",
    "X_MIN   = 0    # Lower limit of the distributions\n",
    "X_MAX   = 1    # Higher limit of the distributions\n",
    "STD_MIN = 0.01 # Minimal standard deviation of the distributions\n",
    "STD_MAX = 0.2  # Maximal standard deviation of the distributions\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9d2016ec",
   "metadata": {},
   "outputs": [],
   "source": [
    "## Generate and plot distributions\n",
    "\n",
    "arm_avg         = zeros(K) # Mean value of the distribution for each arm (before truncation)\n",
    "arm_std         = zeros(K) # Standard deviation of the distribution for each arm (before truncation)\n",
    "arm_expectation = zeros(K) # Expectation (i.e., mean value) of the distribution for each arm (after truncation)\n",
    "\n",
    "p = plot(xlabel = \"x\", ylabel = \"pdf(x)\", legend = :topleft)\n",
    "xx = 0:1e-3:1\n",
    "\n",
    "for k in 1:K\n",
    "    \n",
    "    # Randomly sample distribution properties\n",
    "    arm_avg[k] = rand(Uniform(X_MIN, X_MAX))\n",
    "    arm_std[k] = rand(Uniform(STD_MIN, STD_MAX))\n",
    "            \n",
    "    # Construct the normal distribution (arm_avg[k], arm_std[k]) truncated within [X_MIN, X_MAX]\n",
    "    d = Truncated(Normal(arm_avg[k], arm_std[k]), X_MIN, X_MAX)\n",
    " \n",
    "    # Calculate the expected value\n",
    "    arm_expectation[k] = mean(d)\n",
    "    \n",
    "    # Plot distributions\n",
    "    plot!(\n",
    "        xx, pdf.(d, xx),\n",
    "        label = @sprintf(\"Arm %d, Mean = %0.2f, CDF = %0.1f\", k, arm_expectation[k], cdf(d, X_MAX)))\n",
    "\n",
    "end\n",
    "\n",
    "display(p)\n",
    "\n",
    "# Store the maximum expected value\n",
    "max_expected_value = maximum(arm_expectation)\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "7dd38493",
   "metadata": {},
   "outputs": [],
   "source": [
    "## Hyperparameters for the epsilon-greedy strategy\n",
    "\n",
    "epsilon_ini   = 0.5  # Promote exploration instead of exploitation (within [0, 1])\n",
    "epsilon_decay = 0.99 # Incremental decrease of epsilon (within [0, 1])\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f951c05d",
   "metadata": {},
   "outputs": [],
   "source": [
    "## Generate and plot the epsilon-greedy strategy\n",
    "\n",
    "epsilon    = zeros(T)\n",
    "epsilon[1] = epsilon_ini\n",
    "for t in 1:T-1\n",
    "    epsilon[t+1] = epsilon[t] * epsilon_decay\n",
    "end\n",
    "\n",
    "p = plot(xlabel = \"Timesteps\", ylabel = \"epsilon\", legend = :topright)\n",
    "plot!(1:T, epsilon, label = \"epsilon\")\n",
    "display(p)\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4342792e",
   "metadata": {},
   "outputs": [],
   "source": [
    "## Solve the game using the epsilon-greedy Q-learning algorithm\n",
    "\n",
    "nb_pulls_per_arm           = zeros(K) # Store the total number of pulls per arm\n",
    "rewards_cumulated_per_arm  = zeros(K) # Store the total amount of rewards received per arm\n",
    "pulled_arm                 = zeros(T) # Store the index of the arm that was pulled at timestep t (for display purposes)\n",
    "\n",
    "Q                         = zeros(T+1, K) # Quality table (with temporal memory, for display purposes)\n",
    "rewards_instantaneous     = zeros(T+1)    # Instantaneous rewards (for display purposes)\n",
    "rewards_cumulated         = zeros(T+1)    # Cumulated rewards\n",
    "regret                    = zeros(T+1)    # Regret\n",
    "expectation_instantaneous = zeros(T+1)    # Instantaneous expectation (for display purposes)\n",
    "rewards_random_cumulated  = zeros(T+1)    # Cumulated rewards when pulling at random (for comparison purposes)\n",
    "\n",
    "for t in 2:T+1\n",
    "    \n",
    "    if rand(Uniform(0, 1)) < epsilon[t-1]\n",
    "        # Exploration\n",
    "        k_select = rand(1:K)\n",
    "    else\n",
    "        # Exploitation (TODO: randomly break ties)\n",
    "        k_select = mapslices(argmax, Q[t, :], dims = 1)[1]\n",
    "    end\n",
    "    \n",
    "    # Store the index of the arm that was pulled (for display purposes)\n",
    "    pulled_arm[t-1] = k_select\n",
    "    \n",
    "    # Obtain reward\n",
    "    rewards_instantaneous[t] = rand(Truncated(Normal(arm_avg[k_select], arm_std[k_select]), X_MIN, X_MAX), 1)[1]\n",
    "\n",
    "    # Update the known information for the selected arm\n",
    "    rewards_cumulated_per_arm[k_select] += rewards_instantaneous[t]\n",
    "    nb_pulls_per_arm[k_select] += 1\n",
    "    \n",
    "    # Update the quality table\n",
    "    # (For display purpose, pulling one arm updates the present and all future values of the table)\n",
    "    Q[t:(T+1), k_select] .= rewards_cumulated_per_arm[k_select] / nb_pulls_per_arm[k_select]\n",
    "    \n",
    "    # Update the regret\n",
    "    regret[t] = regret[t-1] + max_expected_value - arm_expectation[k_select]  \n",
    "      \n",
    "    # Update the cumulated rewards\n",
    "    rewards_cumulated[t] = rewards_cumulated[t-1] + rewards_instantaneous[t]\n",
    "    \n",
    "    # Update the instantaneous expectation (for display purposes)\n",
    "    expectation_instantaneous[t] = arm_expectation[k_select]\n",
    "    \n",
    "    # Pull one arm at random and store the corresponding results (for comparison purposes)\n",
    "    k_random = rand(1:K)\n",
    "    reward_random_instantaneous = rand(Truncated(Normal(arm_avg[k_random], arm_std[k_random]), X_MIN, X_MAX), 1)[1]\n",
    "    rewards_random_cumulated[t] = rewards_random_cumulated[t-1] + reward_random_instantaneous\n",
    "\n",
    "end\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dbd7d80f",
   "metadata": {},
   "outputs": [],
   "source": [
    "## Plot results\n",
    "\n",
    "maximal_expectation = ones(T+1) * max_expected_value\n",
    "maximal_expectation_cumulated = (0:T) * max_expected_value\n",
    "\n",
    "p = plot(xlabel = \"Timesteps\", ylabel = \"Rewards\", legend = :bottomright)\n",
    "plot!(0:T, rewards_instantaneous, label = \"Instantaneous rewards\")\n",
    "plot!(0:T, expectation_instantaneous, linewidth = 3, label = \"Instantaneous expectation\")\n",
    "plot!(0:T, maximal_expectation, linewidth = 3, label = \"Maximal expectation\")\n",
    "display(p)\n",
    "\n",
    "p = plot(xlabel = \"Timesteps\", ylabel = \"Cumulated rewards\")\n",
    "plot!(\n",
    "    0:T, rewards_cumulated, \n",
    "    label = @sprintf(\"Cumulated rewards = %0.2f\", rewards_cumulated[T]), legend = :topleft)\n",
    "plot!(\n",
    "    0:T, maximal_expectation_cumulated,\n",
    "    label = @sprintf(\"Maximal cumulated expectation = %0.2f\", maximal_expectation_cumulated[T]))\n",
    "plot!(\n",
    "    0:T, regret,\n",
    "    label = @sprintf(\"Regret = %0.2f\", regret[T]))\n",
    "plot!(\n",
    "    0:T, rewards_random_cumulated,\n",
    "    linestyle = :dash,\n",
    "    label = @sprintf(\"Pulling at random = %0.2f\", rewards_random_cumulated[T]))\n",
    "display(p)\n",
    "\n",
    "p = plot(xlabel = \"Timesteps\", ylabel = \"Pulled arm\", legend = :inside)\n",
    "scatter!(1:T, pulled_arm, markerstrokewidth = 0, label = \"Pulled arm\")\n",
    "display(p)\n",
    "\n",
    "p = plot(xlabel = \"Timesteps\", ylabel = \"Quality (estimated expectation)\", legend = :inside)\n",
    "for k in 1:K\n",
    "    plot!(0:T, Q[:, k], label = string(\"Arm \", k))\n",
    "end\n",
    "display(p)\n",
    ";"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.4",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
